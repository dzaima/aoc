#!/usr/bin/env cbqn
⟨Ints⟩ ← •Import "utils.bqn"
i ← ∾{2↕∘‿2⥊𝕩}∘Ints¨•FLines ≠◶"14.in"‿⊑ •args

Set1 ← {𝕨 1⌾((+´𝕩×1«≢𝕨)⊑⥊)↩}

Run ← {
  min ← ¯1+0⊸⌊⌾(1⊸⊑) ⌊˝∾˝𝕩
  l ← -⎉1⟜min 𝕩
  max ← ⌈˝∾˝l
  b ← ⍉(↕2+max) ∊ ∾{(<⊏𝕩)+⥊(<∘××·↕1+|)-˝⌽𝕩}¨ <˘l
  s ← 0⌈⌽500‿0-min

  lx ← ¯1+≠⊏b
  ly ← ¯1+≠b

  Step ← {
    𝕩⊑b? @;
    y‿x:
    {
      y+↩1
      (y=ly)∨(x=0)∨x=lx? @;
      ¬(p←⟨y, x⟩)⊑b? p;
      ¬(p←⟨y, x-1⟩)⊑b? p;
      ¬(p←⟨y, x+1⟩)⊑b? p;
      ⋈𝕩
    }𝕩
  }
  n ← 0
  Drop ← { 𝕊:
    e ← ⊑Step •_while_ (1<≠) s
    e≢@?
      b Set1↩ e
      n+↩1
      1
    ;
      0
  }
  Drop•_while_⊢•_timed 1
  •Show n
}

Run i
ji ← 500‿0∾∾˝i
max ← ⌈˝ji
min ← ⌊˝ji
by ← 2+1⊑max
i2 ← i∾ [⟨by-˜⊑min, by⟩, ⟨by+⊑max, by⟩]
Run i2