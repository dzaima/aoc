#!/usr/bin/env bqn
⟨SplitF, _underSelect_, MDSlash, Input⟩ ← •Import "../../utils/utils.bqn"
a‿b ← ⟨⟩SplitF •FLines 2024‿15 Input •args
a >↩
b ∾↩
{
  c←a
  {
    d ← ⊑𝕩⊐˜">v<^"
    c ⌽∘⍉⍟d↩
    y‿x← ⋈⁼MDSlash c='@'
    c {𝕩↓˜↩1⋄('.'⊐˜𝕩)<○⊑'#'⊐˜𝕩? ".@"∾('O'⥊˜k)∾(1+k←⊑𝕩⊐'.')↓𝕩; 𝕩}_underSelect_⟨y, {x+↕𝕩-x}1⊑≢c⟩↩ # ⌾(x↓y⊏⊢)↩
    c ⌽∘⍉⍟(4|4-d)↩
    1
  }¨ b
  •Show +´∾100‿1⊸×¨ MDSlash c='O'
}

{
  c2 ← 2/˘'.'¨⌾((a='O')⊸(/○⥊)) a
  c2 '.'⌾((1⊑MDSlash c2='@')⊸⊑)↩
  {c2 "[]"⌾(((⊢⋈0‿1⊸+) 𝕩×1‿2)⊸⊑)↩⋄1}¨ MDSlash a='O'
  {
    d ← ⊑𝕩⊐˜">v<^"
    {
      ⊑d∊0‿2?
        c2 ⌽∘⍉⍟d↩
        y‿x← ⋈⁼MDSlash c2='@'
        c2 {𝕩↓˜↩1⋄('.'⊐˜𝕩)<○⊑'#'⊐˜𝕩? ".@"∾(k↑𝕩)∾(1+k←⊑𝕩⊐'.')↓𝕩; 𝕩}_underSelect_⟨y, {x+↕𝕩-x}1⊑≢c2⟩↩ #⌾⌽⌾((1+x)↑y⊏⊢)↩
        c2 ⌽∘⍉⍟(4-d)↩
      ;
        c2 ⌽⍟(d=1)↩
        ma ← c2='@'
        {𝕊:
          t←«𝕩
          t2←t∧c2≠'.'
          t2∨↩ »˘ t2 ∧ c2='['
          t2∨↩ «˘ t2 ∧ c2=']'
          ma∨↩t2
          t2
        }•_while_(∨´⥊) ma
        {𝕊: v0←ma/○⥊c2⋄ c2 '.'¨⌾((/⥊ma)⊏⥊)↩ ⋄ c2 v0⌾((/⥊«ma)⊏⥊)↩}⍟⊢ ∧´'#'≠(«ma)/○⥊c2
        c2 ⌽⍟(d=1)↩
    }
    1
  }¨ b
  •Show +´∾100‿1⊸×¨ MDSlash c2='['
}